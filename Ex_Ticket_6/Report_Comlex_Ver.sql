ВЫБРАТЬ
	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	ОстаткиНоменклатурыОстатки.Склад КАК Склад,
	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток
ПОМЕСТИТЬ втОстаткиКомплектующих
ИЗ
	РегистрНакопления.ОстаткиНоменклатуры.Остатки КАК ОстаткиНоменклатурыОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	СоставСтеллажей.Стеллаж КАК Стеллаж,
	СоставСтеллажей.Комплектующее КАК Комплектующее,
	СоставСтеллажей.Количество КАК Количество,
	Склады.Ссылка КАК Склады
ПОМЕСТИТЬ втСоставСтеллажейСоСкладами
ИЗ
	РегистрСведений.СоставСтеллажей КАК СоставСтеллажей,
	Справочник.Склады КАК Склады
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втСоставСтеллажейСоСкладами.Стеллаж КАК Стеллаж,
	втСоставСтеллажейСоСкладами.Комплектующее КАК Комплектующее,
	ЕСТЬNULL(втОстаткиКомплектующих.КоличествоОстаток / втСоставСтеллажейСоСкладами.Количество, 0) КАК Количество,
	втСоставСтеллажейСоСкладами.Склады КАК Склад
ПОМЕСТИТЬ втСоставСтеллажей
ИЗ
	втСоставСтеллажейСоСкладами КАК втСоставСтеллажейСоСкладами
		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиКомплектующих КАК втОстаткиКомплектующих
		ПО втСоставСтеллажейСоСкладами.Комплектующее = втОстаткиКомплектующих.Номенклатура
			И втСоставСтеллажейСоСкладами.Склады = втОстаткиКомплектующих.Склад
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втСоставСтеллажей.Стеллаж КАК Стеллаж,
	втСоставСтеллажей.Склад КАК Склад,
	МИНИМУМ(втСоставСтеллажей.Количество) КАК Количество
ИЗ
	втСоставСтеллажей КАК втСоставСтеллажей

СГРУППИРОВАТЬ ПО
	втСоставСтеллажей.Стеллаж,
	втСоставСтеллажей.Склад

ИМЕЮЩИЕ
	МИНИМУМ(втСоставСтеллажей.Количество) > 0