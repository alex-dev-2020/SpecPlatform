
Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	РасходнаяНакладнаяСписокТоваров.Номенклатура КАК Номенклатура,
			|	СУММА(РасходнаяНакладнаяСписокТоваров.Количество) КАК КоличествоДок
			|ПОМЕСТИТЬ ВремТаб
			|ИЗ
			|	Документ.РасходнаяНакладная.СписокТоваров КАК РасходнаяНакладнаяСписокТоваров
			|ГДЕ
			|	РасходнаяНакладнаяСписокТоваров.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	РасходнаяНакладнаяСписокТоваров.Номенклатура
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВремТаб.Номенклатура КАК Номенклатура,
			|	ВремТаб.КоличествоДок КАК КоличествоДок,
			|	ОстаткиНоменклатурыОстатки.Склад,
			|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
			|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
			|	ВремТаб.Номенклатура.Представление
			|ИЗ
			|	ВремТаб КАК ВремТаб
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
			|			&МомВремени,
			|			Номенклатура В
			|				(ВЫБРАТЬ
			|					ВремТаб.Номенклатура
			|				ИЗ
			|					ВремТаб КАК ВремТаб)) КАК ОстаткиНоменклатурыОстатки
			|		ПО ВремТаб.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
			|
			|УПОРЯДОЧИТЬ ПО
			|	ОстаткиНоменклатурыОстатки.Склад.Приоритет
			|ИТОГИ
			|	МАКСИМУМ(КоличествоДок),
			|	СУММА(КоличествоОстаток)
			|ПО
			|	Номенклатура";

		Запрос.УстановитьПараметр("МомВремени", МоментВремени());
		Запрос.УстановитьПараметр("Ссылка", Ссылка);

		Результат = Запрос.Выполнить();

		ВыборкаНоменклатура = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаНоменклатура.Следующий() Цикл
			Если ВыборкаНоменклатура.КоличествоДок > ВыборкаНоменклатура.КоличествоОстаток Тогда
			    Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = 
"ОПЕР.: по номенклатуре " + ВыборкаНоменклатура.НоменклатураПредставление + 
					" не хватает товара. Реально есть " + ВыборкаНоменклатура.КоличествоОстаток;

				Сообщение.Сообщить(); 
				
			    Отказ = Истина;
				Продолжить;
			КонецЕсли; 

			Если Не Отказ Тогда
				//Списание "по партиям"
				
				ОсталосьСписать = ВыборкаНоменклатура.КоличествоДок;
				
				ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
				Пока ОсталосьСписать > 0 И ВыборкаДетальныеЗаписи.Следующий() Цикл
					// регистр ОстаткиНоменклатуры Расход
					Движение = Движения.ОстаткиНоменклатуры.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
					Движение.Склад = ВыборкаДетальныеЗаписи.Склад;
					
					Движение.Количество = Мин(ОсталосьСписать, 
									ВыборкаДетальныеЗаписи.КоличествоОстаток);
					
					//Проблема копеек
					СуммаСписания = ?(Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток, 
						ВыборкаДетальныеЗаписи.СуммаОстаток,
						ВыборкаДетальныеЗаписи.СуммаОстаток / 
							ВыборкаДетальныеЗаписи.КоличествоОстаток * Движение.Количество);
					
					Движение.Сумма = СуммаСписания;
					
					ОсталосьСписать = ОсталосьСписать - Движение.Количество;
				КонецЦикла;
			КонецЕсли; 
		КонецЦикла;
 
